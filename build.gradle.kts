import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    java
    id("com.google.devtools.ksp")
    id("org.springframework.boot")
    id("io.spring.dependency-management")
    id("org.hibernate.orm")
    id("org.graalvm.buildtools.native")
    kotlin("jvm")
    kotlin("plugin.spring")
    kotlin("plugin.jpa")
    kotlin("kapt")
}
//ì§ì ‘ í• ë‹¹ ê³ ì •ê°’
// ì„ ì–¸ê³¼ ë™ì‹œì— ê°’ì´ ê²°ì •ë˜ëŠ” `ì¦‰ì‹œ ì´ˆê¸°í™”`
// ì»´íŒŒì¼ëŸ¬ê°€ String ì¶”ë¡ í•˜ë¯€ë¡œ íƒ€ì… x
//val releaseVer = "v0.0.1"

// property delegation ì‚¬ìš© - runtimeì‹œ í”„ë¡œí¼í‹°ì—ì„œ ê°’ì„ ê°€ì ¸ì˜´
// `:` íƒ€ì…ì„ ëª…ì‹œì ìœ¼ë¡œ ì„ ì–¸ - ì»´íŒŒì¼ëŸ¬ê°€ íƒ€ì… ì¶”ë¡ ì„ ëª»í•˜ë¯€ë¡œ
// ì™¸ë¶€ propertiesì—ì„œ ê°’ì„ ê°€ì ¸ì˜¤ëŠ” delegation
val springBootVersion: String by project
val jpaVersion: String by project
val kotestVersion: String by project
val mockkVersion: String by project
val springMockKVersion: String by project
val mapstructVersion: String by project
val mapstructSpringVersion: String by project
val group: String by project
val releaseVer: String by project
val profile = System.getProperty("spring.profiles.active")
    ?: System.getenv("SPRING_PROFILES_ACTIVE")
    ?: "local"

version =
    "$releaseVer-${LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMdd_HHmmss"))}"
description = "Get-your-hands-dirty-on-clean-architecture"

configurations {
    compileOnly { extendsFrom(configurations.annotationProcessor.get()) }
    testCompileOnly { extendsFrom(configurations.testAnnotationProcessor.get()) }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))

    // spring AP - Java + Kotlin ëª¨ë‘ ì§€ì›
    // TODO: Kotlin ë§ˆì´ê·¸ë ˆì´ì…˜ ì™„ë£Œ ì‹œ kaptë§Œ ì‚¬ìš©
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    kapt("org.springframework.boot:spring-boot-configuration-processor")

    // spring starter
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-actuator")

    // JPA
    implementation("jakarta.persistence:jakarta.persistence-api:$jpaVersion")

    // Kotlin - BOMì—ì„œ ê´€ë¦¬ë˜ëŠ” ë²„ì „ ì‚¬ìš©
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    // Test - Spring Boot BOMì´ ê´€ë¦¬
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // BOMì—ì„œ ê´€ë¦¬í•˜ì§€ ì•ŠëŠ” ë¼ì´ë¸ŒëŸ¬ë¦¬ë“¤ë§Œ ë²„ì „ ëª…ì‹œ
    testImplementation("com.tngtech.archunit:archunit-junit5-engine:1.4.1")
    testImplementation("org.mockito.kotlin:mockito-kotlin:6.0.0")



    // MapStruct Core
    implementation("org.mapstruct:mapstruct:${mapstructVersion}")
    // TODO: Kotlin ë§ˆì´ê·¸ë ˆì´ì…˜ ëë‚˜ê³  í–¥í›„ Lombok ì œê±° ì‹œ kaptë§Œ ë‚¨ê¸°ê³  ì •ë¦¬í•  ê²ƒ
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapstructVersion}")
    testAnnotationProcessor("org.mapstruct:mapstruct-processor:${mapstructVersion}")
    kapt("org.mapstruct:mapstruct-processor:${mapstructVersion}")

    // MapStruct Spring Extensions
    implementation("org.mapstruct.extensions.spring:mapstruct-spring-annotations:${mapstructSpringVersion}")
    implementation("org.mapstruct.extensions.spring:mapstruct-spring-extensions:${mapstructSpringVersion}")
    kapt("org.mapstruct.extensions.spring:mapstruct-spring-extensions:${mapstructSpringVersion}")

    // MapStruct Test only
    testImplementation("org.mapstruct.extensions.spring:mapstruct-spring-test-extensions:${mapstructSpringVersion}")

    // TODO: kotlin ë§ˆì´ê·¸ë ˆì´ì…˜ì‹œ lombok, binding ì „ì²´ ì œê±°
    // Lombok(mapStruct ë’¤ì— ì™€ì•¼ í•¨)
    compileOnly("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")

    // Lombokê³¼ MapStruct í†µí•©
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding:0.2.0")
    testAnnotationProcessor("org.projectlombok:lombok-mapstruct-binding:0.2.0")

//    Kotlin í…ŒìŠ¤íŠ¸ ë¼ì´ë¸ŒëŸ¬ë¦¬
//    Kotest í…ŒìŠ¤íŠ¸ í”„ë ˆì„ì›Œí¬ëŠ” JVM, Android, ìë°”ìŠ¤í¬ë¦½íŠ¸ ë° ë„¤ì´í‹°ë¸Œ í™˜ê²½ì—ì„œ ì§€ì›ë©ë‹ˆë‹¤.
    testImplementation("io.kotest:kotest-assertions-core:$kotestVersion")
    testImplementation("io.kotest:kotest-property:$kotestVersion")
    testImplementation("io.kotest:kotest-runner-junit5:$kotestVersion")
    testImplementation("io.kotest:kotest-framework-api-jvm:${kotestVersion}")
    testImplementation("io.kotest.extensions:kotest-extensions-spring:1.3.0")
    testImplementation("io.mockk:mockk:$mockkVersion")
    testImplementation("com.ninja-squad:springmockk:$springMockKVersion")

    // dev only
    developmentOnly("org.springframework.boot:spring-boot-devtools")

    // DB - BOMì—ì„œ ë²„ì „ ê´€ë¦¬
    // h2database
    // main DB :  (dev, demo, prod)
    runtimeOnly("com.h2database:h2")

// testcontainers
/*
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.testcontainers:kafka")
    testImplementation("org.testcontainers:jdbc")
    testImplementation("org.testcontainers:mariadb")
    testImplementation("org.testcontainers:mongodb")
    testImplementation("org.testcontainers:mysql")
    testImplementation("org.testcontainers:ollama")
    testImplementation("org.testcontainers:postgresql")
    testImplementation("org.testcontainers:cassandra")
    testImplementation("org.testcontainers:selenium")
    testImplementation("org.testcontainers:vault")
    testImplementation("org.testcontainers:mockserver")
    testImplementation("org.testcontainers:nginx")
    testImplementation("org.testcontainers:consul")
    testImplementation("org.testcontainers:influxdb")
    testImplementation("org.testcontainers:activemq")
    testImplementation("org.testcontainers:grafana")
    testImplementation("org.testcontainers:docker-compose")
    testImplementation("org.testcontainers:ldap")
    testImplementation("org.testcontainers:jdbc-test")
    testImplementation("org.testcontainers:docs-examples")
    testImplementation("org.testcontainers:k6")

    testImplementation("org.testcontainers:r2dbc")
    testImplementation("org.testcontainers:rabbitmq")
*/

}

kapt {
    keepJavacAnnotationProcessors = true  // Java AP ë³‘í–‰ ì‹¤í–‰ (Lombok ì²˜ë¦¬ìš©)
    correctErrorTypes = true              // íƒ€ì… ì—ëŸ¬ ì •í™•ë„ í–¥ìƒ
    arguments {
        arg("mapstruct.defaultComponentModel", "spring")
        arg("mapstruct.defaultInjectionStrategy", "constructor")
        // local, dev mode
        // TODO: prodì—ì„œëŠ” ERRORë¡œ ê°•í™” ê³ ë ¤

        arg("mapstruct.unmappedSourcePolicy", "WARN")
        arg("mapstruct.unmappedTargetPolicy", "WARN")
        arg("mapstruct.verbose", "true")
        arg("mapstruct.suppressGeneratorTimestamp", "true")
        arg("mapstruct.suppressGeneratorVersionInfoComment", "true")
        arg("mapstruct.defaultNullValuePropertyMappingStrategy", "SET_TO_NULL")
    }
}

hibernate {
    enhancement {
        // Hibernate 7.xì—ì„œ deprecated
        //  ì„±ëŠ¥ ìµœì í™”ë¥¼ ìœ„í•´ ë¹„í™œì„±í™”
        enableAssociationManagement = false
    }
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
}

// JAR íƒœìŠ¤í¬: ì¤‘ë³µ íŒŒì¼ ì²˜ë¦¬ ì „ëµ (KAPT + annotationProcessor ë³‘í–‰ ì‹œ)
tasks.named<Jar>("jar") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named<Jar>("bootJar") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// ì¼ë°˜ Java ì»´íŒŒì¼ì—ì„œëŠ” ê²½ê³  í™œì„±í™”
tasks.named("compileJava", JavaCompile::class) {
    options.compilerArgs.add("-Xlint:unchecked")
}

// AOT ì»´íŒŒì¼ íƒœìŠ¤í¬ì—ì„œëŠ” ìƒì„±ëœ ì½”ë“œì˜ ê²½ê³  ì™„ì „ ì œê±°
tasks.named("compileAotJava", JavaCompile::class) {
    options.compilerArgs.addAll(listOf(
        "-Xlint:none"  // ëª¨ë“  ê²½ê³  ì™„ì „ ì œê±°
    ))
}

tasks.withType<Test> {
    // Test ìœ í˜•ì˜ ëª¨ë“  í…ŒìŠ¤íŠ¸ task ê³µí†µ configure ìš©
}
tasks.named<Test>("test") {
    useJUnitPlatform()
    maxParallelForks = Runtime.getRuntime().availableProcessors()
    testLogging {
        events("passed", "skipped", "failed")
        if (profile == "local" || profile == "dev") {
            // ìƒì„¸ ë¡œê·¸
            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
            showStackTraces = true
            showCauses = true
            showExceptions = true
            showStandardStreams = true
            println("ğŸ” í…ŒìŠ¤íŠ¸ ìƒì„¸ ë¡œê·¸ í™œì„±í™” (profile: $profile)")
        } else {
            // ê°„ë‹¨í•œ ë¡œê·¸ (demo, prod)
            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.SHORT
            showStackTraces = false
            println("ğŸ“ í…ŒìŠ¤íŠ¸ ê°„ë‹¨ ë¡œê·¸ (profile: $profile)")
        }
    }
}
tasks.named("processTestAot").configure {
    enabled = false
}

kotlin {
    jvmToolchain(24)
    compilerOptions {
        freeCompilerArgs.add("-Xjsr305=strict")             //  JSR-305 ì• ë…¸í…Œì´ì…˜ì˜ null ì•ˆì •ì„± ì–´ë…¸í…Œì´ì…˜ì„ ì—„ê²©
        freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn") // ì‹¤í—˜ì  APIë“± APIë¥¼ ì‚¬ìš©í•  ë•Œ í•´ë‹¹ ì˜µíŠ¸ì¸ ì–´ë…¸í…Œì´ì…˜ ì‚¬ìš©ì„ í—ˆìš©
        freeCompilerArgs.add("-Xjvm-default=all-compatibility") // Java ìƒí˜¸ìš´ìš©ì„± ê°œì„ 
        allWarningsAsErrors = true
        jvmTarget.set(JvmTarget.JVM_24)
        languageVersion.set(KotlinVersion.KOTLIN_2_2)
        apiVersion.set(KotlinVersion.KOTLIN_2_2)
    }
}

configurations.all {
    resolutionStrategy {
        // CVE-2025-48924 ë³´ì•ˆ ì·¨ì•½ì  í•´ê²°
        force("org.apache.commons:commons-lang3:3.18.0")

        // ìºì‹œ
        // prod: í•˜ë£¨ì— í•œ ë²ˆë§Œ ì²´í¬
//        cacheDynamicVersionsFor(24, TimeUnit.HOURS)
//        cacheChangingModulesFor(24, TimeUnit.HOURS)

        // dev ì„œë²„ìš©: 10ë¶„ë§ˆë‹¤ ìƒˆ ë²„ì „ ì²´í¬ (ë„ˆë¬´ ìì£¼ ì²´í¬í•˜ë©´ ë¹Œë“œ ëŠë¦¼)
//        cacheDynamicVersionsFor(10, TimeUnit.MINUTES)
        // SNAPSHOTì€ 5ë¶„ë§ˆë‹¤ ì²´í¬
//        cacheChangingModulesFor(5, TimeUnit.MINUTES)
    }
}